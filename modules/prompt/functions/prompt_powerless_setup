CURRENT_BG='NONE'
CURRENT_REV_BG='NONE'
SEGMENT_SEPARATOR=' '
SEGMENT_REV_SEPARATOR=' '

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
function prompt_powerless_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

function prompt_powerless_segment_rev() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $1 != $CURRENT_REV_BG  && $1 != "default" ]]; then
    echo -n " %{%F{$1}%}$SEGMENT_REV_SEPARATOR%{$bg%}%{$fg%}"
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_REV_BG=$1
  [[ -n $3 ]] && echo -n $3
}

# End the prompt, closing any open segments
function prompt_powerless_end() {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%} "
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
function prompt_powerless_context() {
  local user=`whoami`

  if [[ "$user" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    prompt_powerless_segment black default "%(!.%{%F{red}%}.)$user@%m"
  fi
}

# Git: branch/detached head, dirty status
function prompt_powerless_git() {
  local ref dirty ahead behind
  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    dirty=$git_info[dirty]
    ahead=$git_info[ahead]
    behind=$git_info[behind]
    ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="➦ $(git show-ref --head -s --abbrev |head -n1 2> /dev/null)"
    if [[ $dirty != "" ]]; then
      prompt_powerless_segment default yellow
    elif [[ $ahead != "" ]] || [[ $behind != "" ]]; then
      if [[ $ahead != "" ]]; then
	prompt_powerless_segment default blue
	GIT_CLEAN_COLOR=$GIT_LCL_CLEAN_COLOR
      else
	prompt_powerless_segment default red
      fi
    else
      prompt_powerless_segment default green
      GIT_CLEAN_COLOR=$GIT_REM_CLEAN_COLOR
    fi
    echo -n "${ref/refs\/heads\//B }$git_info[symbols]"
  fi
}

function prompt_powerless_git_user() {
  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    local gitrepuser="$(git config --local user.email)"
    local gitglobuser="$(git config --global user.email)"
    local gituser="${GIT_AUTHOR_EMAIL:-${gitrepuser:-${gitglobuser}}}"
    if [[ ${gituser} == 'oliver@weyhmueller.de' || ${gituser} == '' ]]; then
      gituser="oli"
      prompt_powerless_segment default green
    elif [[ ${gituser} == 'oliver.weyhmueller@t-systems.com' ]]; then
      gituser="tsi"
      prompt_powerless_segment default magenta
    else
      gituser="???"
      prompt_powerless_segment default red
    fi
    echo -n "$gituser"
  fi
}

# Dir: current working directory
function prompt_powerless_dir() {
  prompt_powerless_segment "" blue "${PWD/#$HOME/~}"
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
function prompt_powerless_status() {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}X $RETVAL"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}R"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}J"

  [[ -n "$symbols" ]] && prompt_powerless_segment default default "$symbols"
}

function prompt_powerless_time() {
  prompt_powerless_segment_rev default cyan '%D{%H:%M:%S}'
}

function prompt_powerless_date() {
  prompt_powerless_segment_rev default default '%D{%Y-%m-%d}'
}

function prompt_powerless_history() {
  prompt_powerless_segment_rev default green '[%h]'
}

## Main prompt
function prompt_powerless_build_prompt() {
  RETVAL=$?
  prompt_powerless_status
  prompt_powerless_context
  prompt_powerless_dir
  prompt_powerless_git_user
  prompt_powerless_git
  prompt_powerless_end
}

## Rev Prompt
function prompt_powerless_build_rev_prompt() {
  prompt_powerless_time
  prompt_powerless_date
  prompt_powerless_history
}

function prompt_powerless_setup() {
SEGMENT_SEPARATOR=' '
SEGMENT_REV_SEPARATOR=' '

  add-zsh-hook precmd prompt_powerless_precmd
  GIT_DIRTY_COLOR=%F{red}
  GIT_LCL_CLEAN_COLOR=%F{green}
  GIT_REM_CLEAN_COLOR=%F{black}


  zstyle ':prezto:module:editor:info:completing' format '%F{blue}...%f'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymo p:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '+'
  zstyle ':prezto:module:git:info:ahead' format 'A '
  zstyle ':prezto:module:git:info:behind' format 'B '
  zstyle ':prezto:module:git:info:dirty' format '%D'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format 'X'
  zstyle ':prezto:module:git:info:modified' format '*'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format '>'
  zstyle ':prezto:module:git:info:stashed' format 'S'
  zstyle ':prezto:module:git:info:unmerged' format '═'
  zstyle ':prezto:module:git:info:untracked' format ' ?'
  zstyle ':prezto:module:git:info:keys' format \
    'dirty' '%D' \
    'ahead' '%A' \
    'behind' '%B' \
    'symbols' ' %A%B%S%a%d%m%r%U%u'

  PROMPT='%{%f%b%k%}$(prompt_powerless_build_prompt)'

  if [ "$POWERLINE_NO_BLANK_LINE" = "" ]; then
      PROMPT="
"$PROMPT
  fi
  RPROMPT='%{%f%b%k%}$(prompt_powerless_build_rev_prompt)'
}

function prompt_powerless_precmd() {
  if (( $+functions[git-info] )); then
    git-info
  fi
}

prompt_powerless_setup $@
