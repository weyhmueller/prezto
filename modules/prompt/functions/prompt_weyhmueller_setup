CURRENT_BG='NONE'
CURRENT_REV_BG='NONE'
SEGMENT_SEPARATOR='⮀'
SEGMENT_REV_SEPARATOR='⮂'

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
function prompt_weyhmueller_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

function prompt_weyhmueller_segment_rev() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $1 != $CURRENT_REV_BG  && $1 != "default" ]]; then
    echo -n " %{%F{$1}%}$SEGMENT_REV_SEPARATOR%{$bg%}%{$fg%}"
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_REV_BG=$1
  [[ -n $3 ]] && echo -n $3
}

# End the prompt, closing any open segments
function prompt_weyhmueller_end() {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%}"
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
function prompt_weyhmueller_context() {
  local user=`whoami`

  if [[ "$user" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    prompt_weyhmueller_segment black default "%(!.%{%F{red}%}.)$user@%m"
  fi
}

# Git: branch/detached head, dirty status
function prompt_weyhmueller_git() {
  local ref dirty ahead behind
  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    dirty=$git_info[dirty]
    ahead=$git_info[ahead]
    behind=$git_info[behind]
    ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="➦ $(git show-ref --head -s --abbrev |head -n1 2> /dev/null)"
    if [[ $dirty != "" ]]; then
      prompt_weyhmueller_segment yellow black
    elif [[ $ahead != "" ]] || [[ $behind != "" ]]; then
      if [[ $ahead != "" ]]; then
	prompt_weyhmueller_segment blue black
	GIT_CLEAN_COLOR=$GIT_LCL_CLEAN_COLOR
      else
	prompt_weyhmueller_segment red black
      fi
    else
      prompt_weyhmueller_segment green black
      GIT_CLEAN_COLOR=$GIT_REM_CLEAN_COLOR
    fi
    echo -n "${ref/refs\/heads\//⭠ }$git_info[symbols]"
  fi
}

function prompt_weyhmueller_git_user() {
  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    local gitrepuser="$(git config --local user.email)"
    local gitglobuser="$(git config --global user.email)"
    local gituser="${GIT_AUTHOR_EMAIL:-${gitrepuser:-${gitglobuser}}}"
    if [[ ${gituser} == 'oliver@weyhmueller.de' || ${gituser} == '' ]]; then
      gituser="oli"
      prompt_weyhmueller_segment green black
    elif [[ ${gituser} == 'oliver.weyhmueller@t-systems.com' ]]; then
      gituser="tsi"
      prompt_weyhmueller_segment magenta black
    else
      gituser="???"
      prompt_weyhmueller_segment red black
    fi
    echo -n "$gituser"
  fi
}

# Dir: current working directory
function prompt_weyhmueller_dir() {
  prompt_weyhmueller_segment blue black "${PWD/#$HOME/~}"
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
function prompt_weyhmueller_status() {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}✘ $RETVAL"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}⚙"

  [[ -n "$symbols" ]] && prompt_weyhmueller_segment black default "$symbols"
}

function prompt_weyhmueller_time() {
  prompt_weyhmueller_segment_rev default cyan '%D{%H:%M:%S}'
}

function prompt_weyhmueller_date() {
  prompt_weyhmueller_segment_rev default default '%D{%Y-%m-%d}'
}

function prompt_weyhmueller_history() {
  prompt_weyhmueller_segment_rev green black '[%h]'
}

## Main prompt
function prompt_weyhmueller_build_prompt() {
  RETVAL=$?
  prompt_weyhmueller_status
  prompt_weyhmueller_context
  prompt_weyhmueller_dir
  prompt_weyhmueller_git_user
  prompt_weyhmueller_git
  prompt_weyhmueller_end
}

## Rev Prompt
function prompt_weyhmueller_build_rev_prompt() {
  prompt_weyhmueller_time
  prompt_weyhmueller_date
  prompt_weyhmueller_history
}

function prompt_weyhmueller_setup() {

  add-zsh-hook precmd prompt_weyhmueller_precmd

  if [ "$POWERLINE_RIGHT_B" = "" ]; then
    POWERLINE_RIGHT_B=%D{%H:%M:%S}
  fi

  if [ "$POWERLINE_RIGHT_A" = "" ]; then
    POWERLINE_RIGHT_A=%D{%Y-%m-%d}
  fi

  if [ "$POWERLINE_FULL_CURRENT_PATH" = "" ]; then
    POWERLINE_CURRENT_PATH="%1~"
  fi

  POWERLINE_GIT_INFO_LEFT=""
  POWERLINE_GIT_INFO_RIGHT="%F{red}"$'\u2b82'"%F{black}%K{red}"$'$(git_prompt_info)'" %f"
  if [ "$POWERLINE_SHOW_GIT_ON_RIGHT" = "" ]; then
      POWERLINE_GIT_INFO_LEFT=$'$(prompt_git_user)$(prompt_git)'
      POWERLINE_GIT_INFO_RIGHT=""
  fi

  POWERLINE_COLOR_BG_GRAY=%K{240}
  POWERLINE_COLOR_BG_LIGHT_GRAY=%K{240}
  POWERLINE_COLOR_BG_WHITE=%K{255}

  POWERLINE_COLOR_FG_GRAY=%F{240}
  POWERLINE_COLOR_FG_LIGHT_GRAY=%F{240}
  POWERLINE_COLOR_FG_WHITE=%F{255}

  GIT_DIRTY_COLOR=%F{red}
  GIT_LCL_CLEAN_COLOR=%F{green}
  GIT_REM_CLEAN_COLOR=%F{black}
  GIT_PROMPT_INFO=%F{012}


  zstyle ':prezto:module:editor:info:completing' format '%F{blue}...%f'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymo p:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '✚'
  zstyle ':prezto:module:git:info:ahead' format '⬆ '
  zstyle ':prezto:module:git:info:behind' format '⬇ '
  zstyle ':prezto:module:git:info:dirty' format '%D'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format '✖'
  zstyle ':prezto:module:git:info:modified' format '✱'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format '➜'
  zstyle ':prezto:module:git:info:stashed' format '✭'
  zstyle ':prezto:module:git:info:unmerged' format '═'
  zstyle ':prezto:module:git:info:untracked' format ' ◼'
  zstyle ':prezto:module:git:info:keys' format \
    'dirty' '%D' \
    'ahead' '%A' \
    'behind' '%B' \
    'symbols' ' %A%B%S%a%d%m%r%U%u'


  ZSH_THEME_GIT_PROMPT_PREFIX=" \u2b60 "
  ZSH_THEME_GIT_PROMPT_SUFFIX="$GIT_PROMPT_INFO"
  ZSH_THEME_GIT_PROMPT_DIRTY=" $GIT_DIRTY_COLOR✘"
  ZSH_THEME_GIT_PROMPT_CLEAN=" $GIT_CLEAN_COLOR✔"

  PROMPT='%{%f%b%k%}$(prompt_weyhmueller_build_prompt)'

  if [ "$POWERLINE_NO_BLANK_LINE" = "" ]; then
      PROMPT="
  "$PROMPT
  fi
  # RPROMPT=$POWERLINE_GIT_INFO_RIGHT$POWERLINE_COLOR_FG_WHITE$'\u2b82'"%f$POWERLINE_COLOR_BG_WHITE $POWERLINE_COLOR_FG_GRAY$POWERLINE_RIGHT_B "$'\u2b82'"%f%k$POWERLINE_COLOR_BG_GRAY$POWERLINE_COLOR_FG_WHITE $POWERLINE_RIGHT_A %f%k"
  RPROMPT='%{%f%b%k%}$(prompt_weyhmueller_build_rev_prompt)'
}

function prompt_weyhmueller_precmd() {
  if (( $+functions[git-info] )); then
    git-info
  fi
}

prompt_weyhmueller_setup $@
